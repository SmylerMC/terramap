buildscript {
    repositories {
        maven {
            url = "https://maven.minecraftforge.net"
        }
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1.+', changing: true
    }
}
apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'checkstyle'
apply plugin: 'eclipse'
apply plugin: 'maven-publish'


version = project.mod_version
group = project.mod_group
archivesBaseName = project.mod_id

java.toolchain.languageVersion = JavaLanguageVersion.of(8)

minecraft {

    mappings channel: project.mappings_channel, version: project.mappings_version

    runs {
        client {
            workingDirectory project.file('run')

            // We don't do much with registries, let's let them be
            //property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'

            property 'forge.logging.console.level', 'debug'

            // Load the necessary coremods.
            // IMPORTANT: For some reasons, MIXIN adds CC as a reparseable core mod when it loads, which causes FML to not load the regular CC mod.
            // A workaround to that is to trick MixinPlatformAgentFMLLegacy#isIgnoredReparseable(), either with a breakpoint, or with a symlink in the gradle cache directory
            property 'fml.coreMods.load', 'io.github.opencubicchunks.cubicchunks.core.asm.coremod.CubicChunksCoreMod,io.github.opencubicchunks.cubicchunks.cubicgen.asm.coremod.CubicGenCoreMod'
            property 'mixin.env.disableRefMap', 'true'

            mods {
                terramap {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run')
            property 'forge.logging.console.level', 'debug'
            property 'fml.coreMods.load', 'io.github.opencubicchunks.cubicchunks.core.asm.coremod.CubicChunksCoreMod,io.github.opencubicchunks.cubicchunks.cubicgen.asm.coremod.CubicGenCoreMod'
            property 'mixin.env.disableRefMap', 'true'
            mods {
                terramap {
                    source sourceSets.main
                }
            }
        }
    }
}

repositories {
	maven {
   		name = "JitPack"
        url = "https://jitpack.io/"
    }
    maven { //used for leveldb
        name = "DaPorkchop_"
        url = "https://maven.daporkchop.net/"
    }
    maven { //used for leveldb
        name = "OpenCollab Snapshots"
        url = "https://repo.opencollab.dev/snapshot/"
    }
    mavenCentral()
}

dependencies {

    minecraft 'net.minecraftforge:forge:' + project.minecraft_version + '-' + project.forge_version

    implementation fg.deobf("com.github.OpenCubicChunks.CubicChunks:cubicchunks:355923c35ca350482a06ce120fd01474696cd86d") {
        transitive = false
    }
    implementation fg.deobf("com.github.OpenCubicChunks:CubicWorldGen:27de56d2f792513873584b2f8fd9f3082fb259ec") {
        transitive = false
    }
    implementation fg.deobf("com.github.BuildTheEarth:terraplusplus:a48a5a2054f4655d230711c3c68af3ad3b476db4") {
        transitive = true
    }
    runtimeOnly "org.apache.commons:commons-imaging:1.0-alpha2"
    runtimeOnly("net.daporkchop:leveldb-mcpe-jni:0.0.10-SNAPSHOT")  {
        exclude group: "io.netty"
    }
    testImplementation 'org.junit.jupiter:junit-jupiter:5.8.1'
    testImplementation fg.deobf("com.github.BuildTheEarth:terraplusplus:a48a5a2054f4655d230711c3c68af3ad3b476db4")
    
}

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft_version
    duplicatesStrategy = 'include'

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
                
        // replace version and mcversion
        expand 'version': project.version, 'mcversion': project.minecraft_version
    }
        
    // copy everything else except the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

// Example for how to get properties into the manifest for reading at runtime.
jar {
    manifest {
        attributes([
                "Specification-Title"     : project.mod_id,
                "Specification-Vendor"    : project.mod_authors,
                "Specification-Version"   : project.mod_version,
                "Implementation-Title"    : project.mod_id,
                "Implementation-Version"  : project.jar.archiveVersion,
                "Implementation-Vendor"   : project.mod_authors,
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

// Example configuration to allow publishing using the maven-publish plugin
// This is the preferred method to reobfuscate your jar file
jar.finalizedBy('reobfJar')
// However if you are in a multi-project build, dev time needs unobfed jar files, so you can delay the obfuscation until publishing by doing
// publish.dependsOn('reobfJar')

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact jar
        }
    }
    repositories {
        maven {
            url "file://${project.projectDir}/mcmodsrepo"
        }
    }
}

test {
    useJUnitPlatform()
}