buildscript {
    repositories {
        maven {
            url = "https://maven.minecraftforge.net"
        }
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1.+', changing: true
    }
}

apply plugin: 'net.minecraftforge.gradle'

minecraft {
    mappings channel: project.mappings_channel, version: project.mappings_version
}

dependencies {
    minecraft(group: "net.minecraftforge", name: "forge", version: project.minecraft_version + '-' + project.forge_version)
    implementation project(":smylib:core")

    testImplementation project(":smylib:testing")
}

// Build the jar with some sub-modules and a manifest included, then reobfuscate it
jar {
    archiveBaseName = "smylib"
    from project(":smylib:core").sourceSets.main.output
    manifest {
        attributes([
                "Specification-Title"     : project.mod_id,
                "Specification-Vendor"    : project.author,
                "Specification-Version"   : project.version,
                "Implementation-Title"    : project.mod_id,
                "Implementation-Version"  : project.version,
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
    finalizedBy('reobfJar')
}

// This is a dirty hack for a problem that started appearing out of nowhere,
// where Gradle would complain about that task not existing when syncing the project in intelliJ
tasks.register('idePostSync') {}